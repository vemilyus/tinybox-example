plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21' apply false
    id 'org.jetbrains.kotlin.plugin.allopen' version '1.4.21' apply false

    id 'io.quarkus' version '1.11.1.Final' apply false

    id 'io.gitlab.arturbosch.detekt' version '1.16.0-RC1' apply false
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.allopen'

    apply plugin: 'io.gitlab.arturbosch.detekt'

    apply plugin: 'io.quarkus'

    sourceCompatibility = 11
    targetCompatibility = 11

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = [
                    '-Xno-param-assertions',
                    '-Xno-call-assertions'
            ]
            jvmTarget = '11'
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '11'
            javaParameters = true
        }
    }

    allOpen {
        annotation("javax.ws.rs.Path")
        annotation("javax.enterprise.context.ApplicationScoped")
        annotation("io.quarkus.test.junit.QuarkusTest")
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")

        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'io.quarkus:quarkus-kotlin'
        implementation 'io.quarkus:quarkus-arc'
        implementation 'org.apache.camel.quarkus:camel-quarkus-kotlin'

        implementation 'io.quarkus:quarkus-smallrye-openapi'
        implementation 'io.quarkus:quarkus-smallrye-jwt'
        implementation 'io.quarkus:quarkus-smallrye-jwt-build'
        implementation 'io.quarkus:quarkus-kubernetes-client'
        implementation 'io.quarkus:quarkus-kubernetes-config'
        implementation 'io.quarkus:quarkus-resteasy'

        testImplementation 'io.quarkus:quarkus-junit5'
        testImplementation 'io.rest-assured:rest-assured'
    }

    detekt {
        input = files('src/main/kotlin')
        config = files("$rootDir/config/detekt/detekt.yml")

        reports {
            xml {
                enabled = true
                destination = file("$buildDir/reports/detekt.xml")
            }
        }
    }

    test {
        useJUnitPlatform()
        exclude '**/Native*'
    }
}
